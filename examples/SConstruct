
cwd = Dir('..').abspath

import os
import sys
sys.path.insert(0, os.path.abspath(os.path.join(cwd, 'pyol', 'tools')))
import OLBaseConfig

OLBaseConfig.prefix = cwd
config = OLBaseConfig.get_config(ARGLIST)

if config['import_env']:
    env_path = os.environ.get('PATH', '')
    env_ld_library_path = os.environ.get('LD_LIBRARY_PATH', '')
else:
    env_path = []
    env_ld_library_path = []

env = Environment(tools = ['default', config['fortran_tool']],
                  ENV = {"PATH": env_path, "LD_LIBRARY_PATH": env_ld_library_path},
                  CXX = config['cxx'],
                  F90 = config['fortran_compiler'],
                  CCFLAGS = config['ccflags'] + config['generic_optimisation'],
                  FORTRAN = config['fortran_compiler'],
                  F90FLAGS = config['f90_flags'] + config['generic_optimisation'],
                  F90PATH = [os.path.join(cwd, 'lib_src', 'openloops', 'mod')],
                  LIBPATH = [os.path.join(cwd, config['generic_lib_dir'])],
                  LINKFLAGS = config['link_flags'],
                  RPATH = [os.path.join(cwd, config['generic_lib_dir'])])

env.Program('OL_minimal', ['OL_minimal.f90'], LIBS = ['openloops'])
env.Program('OL_fortran', ['OL_fortran.f90'], LIBS = ['openloops'])
env.Program('OL_cpp', ['OL_cpp.cpp'], LIBS = ['openloops'])
env.Program('OL_blha', ['OL_blha.cpp'], LIBS = ['openloops'])
